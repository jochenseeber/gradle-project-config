
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        if(!project.hasProperty("bootstrap")) {
            classpath group: "me.seeber.gradle", name: "gradle-bintray-config", version: "1.1.1"
            classpath group: "me.seeber.gradle", name: "gradle-github-config", version: "1.1.1"
            classpath group: "me.seeber.gradle", name: "gradle-license-config", version: "1.1.1"
            classpath group: "me.seeber.gradle", name: "gradle-plugin-publish-config", version: "1.1.1"
            classpath group: "me.seeber.gradle", name: "gradle-project-config", version: "1.1.1"
        }
    }
}

version = "1.1.1"
group = "me.seeber.gradle"
description = "Default configuration for Gradle builds"

if(project.hasProperty("bootstrap")) {
    apply plugin: "java"
    apply plugin: "groovy"
    apply plugin: "maven"
}
else {
    apply plugin: "me.seeber.distribution.bintray"
    apply plugin: "me.seeber.distribution.gradle-plugin"
    apply plugin: "me.seeber.distribution.maven"
    apply plugin: "me.seeber.ide.eclipse"
    apply plugin: "me.seeber.project.gradle-plugin"
    apply plugin: "me.seeber.repository.github"
    apply plugin: "me.seeber.setup.license"
    apply plugin: "me.seeber.validation.checkstyle"

    model {
        projectConfig {
            organization.name = "Jochen Seeber"
            inceptionYear = 2016
            license {
                id = "BSD 2-Clause"
                exclude "*.prefs"
            }
        }

        githubConfig { user = "jochenseeber" }
    }

    pluginBundle {
        tags = ["build", "configuration"]

        plugins {
            baseProjectPlugin {
                id = "me.seeber.project.base"
                displayName = "Configuration for Gradle Builds"
            }

            mavenDistributionPlugin {
                id = "me.seeber.distribution.maven"
                displayName = "Configuration for Maven Distribution"
            }

            eclipseIdePlugin {
                id = "me.seeber.ide.eclipse"
                displayName = "Configuration for Eclipse IDE"
            }

            gradlePluginProjectPlugin {
                id = "me.seeber.project.gradle-plugin"
                displayName = "Configuration for Gradle Plugin Projects"
            }

            groovyProjectPlugin {
                id = "me.seeber.project.groovy"
                displayName = "Configuration for Groovy Projects"
            }

            javaProjectPlugin {
                id = "me.seeber.project.java"
                displayName = "Configuration for Java Projects"
            }
        }
    }
}

dependencies {
    compile(group: "com.google.guava", name: "guava", version: "22.0")
    compile(group: "gradle.plugin.com.dorongold.plugins", name: "task-tree", version: "1.3")
    compile(group: "net.bytebuddy", name: "byte-buddy", version: "1.5.7")
}

if(project.hasProperty("bootstrap")) {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = "https://plugins.gradle.org/m2" }
        jcenter()
    }
}
